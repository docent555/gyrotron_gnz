C        SUBROUTINE CS
C
C        PURPOSE
C           COMPUTES THE FRESNEL INTEGRALS.
C
C        USAGE
C           CALL CS (C,S,X)
C
C        DESCRIPTION OF PARAMETERS
C           C      - THE RESULTANT VALUE C(X).
C           S      - THE RESULTANT VALUE S(X).
C           X      - THE ARGUMENT OF FRESNEL INTEGRALS
C                    IF X IS NEGATIVE, THE ABSOLUTE VALUE IS USED.
C
C        REMARKS
C           THE ARGUMENT VALUE X REMAINS UNCHANGED.
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           DEFINITION
C           C(X)=INTEGRAL(COS(T)/SQRT(2*PI*T) SUMMED OVER T FROM 0 TO X)
C           S(X)=INTEGRAL(SIN(T)/SQRT(2*PI*T) SUMMED OVER T FROM 0 TO X)
C           EVALUATION
C           USING DIFFERENT APPROXIMATIONS FOR X LESS THAN 4 AND X
C           GREATER THAN 4.
C           REFERENCE
C           'COMPUTATION OF FRESNEL INTEGRALS' BY BOERSMA,
C           MATHEMATICAL TABLES AND OTHER AIDS TO COMPUTATION, VOL. 14,
C           1960, NO. 72, P. 380.
C
C     ..................................................................
C
      SUBROUTINE CS(C,S,X)
      IMPLICIT NONE
      REAL(8) C, S, X, Z, D, A, B
      Z=DABS(X)
    2 IF(Z-4.) 3,3,4
C
C     X IS NOT GREATER THAN 4
C
    3 C=DSQRT(Z)
      S=Z*C
      Z=Z*Z
      C=C*((((((.50998348E-10*Z-.10140729E-7)*Z+.11605284E-5)*Z
     1 -.85224622E-4)*Z+.36938586E-2)*Z-.079788405)*Z+.79788455)
      S=S*(((((-.66777447E-9*Z+.11225331E-6)*Z-.10525853E-4)*Z
     1 +.60435371E-3)*Z-.18997110E-1)*Z+.26596149)
      RETURN
C
C     X IS GREATER THAN 4
C
    4 D=DCOS(Z)
      S=DSIN(Z)
      Z=4./Z
      A=(((((((.87682583E-3*Z-.41692894E-2)*Z+.79709430E-2)*Z-
     1.67928011E-2)*Z-.30953412E-3)*Z+.59721508E-2)*Z-.16064281E-4)*Z-
     2.024933215)*Z-.44440909E-8
      B=((((((-.66339256E-3*Z+.34014090E-2)*Z-.72716901E-2)*Z+
     1.74282459E-2)*Z-.40271450E-3)*Z-.93149105E-2)*Z-.12079984E-5)*Z+
     2.1994711
      Z=DSQRT(Z)
      C=.5+Z*(D*A+S*B)
      S=.5+Z*(S*A-D*B)
      RETURN
      END
